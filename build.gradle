buildscript {
    ext {
        springBootVersion = '2.0.0.RELEASE'
        sonarQubePluginVersion = '2.6.2'
        coverallsPluginVersion = '2.8.2'
    }
    repositories {
        mavenCentral()
        maven { url "https://repo.spring.io/snapshot" }
        maven { url "https://repo.spring.io/milestone" }
    }
    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}"
        classpath "org.sonarsource.scanner.gradle:sonarqube-gradle-plugin:${sonarQubePluginVersion}"
        classpath "org.kt3k.gradle.plugin:coveralls-gradle-plugin:${coverallsPluginVersion}"
    }
}

apply plugin: 'java'
apply plugin: 'groovy'
apply plugin: 'eclipse'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'jacoco'
apply plugin: 'checkstyle'
apply plugin: 'findbugs'
apply plugin: 'pmd'
apply plugin: 'org.sonarqube'
apply plugin: 'com.github.kt3k.coveralls'

group = 'com.lapots.breed.snitch'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = 1.8

repositories {
    mavenCentral()
    jcenter()
    maven { url "https://repo.spring.io/snapshot" }
    maven { url "https://repo.spring.io/milestone" }
}

dependencies {
    // spring boot
    compile('org.springframework.boot:spring-boot-starter')
    compile('org.springframework.boot:spring-boot-starter-webflux')

    compile('com.google.code.findbugs:jsr305:3.0.2') // weird

    compile('org.codehaus.groovy:groovy-all')

    compile('net.bytebuddy:byte-buddy:1.8.0')

    compile('com.google.guava:guava:24.1-jre')

    compileOnly('org.projectlombok:lombok')

    // spock dependencies
    testCompile('org.spockframework:spock-core:1.1-groovy-2.4')
    testCompile('org.spockframework:spock-spring:1.1-groovy-2.4')

    testCompile('org.springframework.boot:spring-boot-starter-test')

    // domain testing
    testCompile('com.openpojo:openpojo:0.8.9')
    testCompile('nl.jqno.equalsverifier:equalsverifier:2.4.3')
}

checkstyle {
    configFile = file('sun_checks.xml')
}

test {
    finalizedBy jacocoTestReport
}

tasks.withType(FindBugs) {
    reports {
        xml.enabled = false
        html.enabled = true
    }
}

findbugs {
    sourceSets = [sourceSets.main]
}

tasks.withType(Pmd) {
    reports {
        html.enabled = true
    }
}

jacocoTestReport {
    reports {
        xml.enabled = true
    }
}

task stage(type: Copy, dependsOn: [clean, build]) {
    from jar.archivePath
    into project.rootDir
    rename {
        'judge-rule-engine.jar'
    }
}
stage.mustRunAfter(clean)

clean << {
    project.file('judge-rule-engine.jar').delete()
}

task run_groovy(type:JavaExec, dependsOn: 'classes') {
    main = 'com.lapots.breed.judge.script'
    classpath = sourceSets.main.runtimeClasspath
}
